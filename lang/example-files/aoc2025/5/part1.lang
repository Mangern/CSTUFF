lft: int[10000];
rgt: int[10000];
nrange: int;

cstr_print: (cstr: *char) -> void = {
    for (c := cstr; c.* != '\0'; c += 1) {
        print(c.*);
    }
}

cstr_parse_int: (cstr: *char, n: int) -> int = {
    ret := 0;

    for (i := 0; i < n; i += 1) {
        ret *= 10;
        ret += cast(int, cstr[i]) - cast(int, '0');
    }
    return ret;
}

readline: (f: File, buf: *char) -> int = {
    tot := 0;
    for (;;) {
        n := file_read(f, buf, 1);
        if (n == 0) {
            return tot;
        }
        if (buf.* == '\n') {
            buf.* = '\0';
            return tot + 1;
        }
        tot += n;
        buf += n;
    }
    println("Unreachable");
    exit(1);
    return -1;
}

main: (argc: int, argv: **char) -> void = {
    if (argc < 2) {
        println("Missing input");
        return;
    }

    infile := file_open(argv[1], "r");
    if (cast(int, infile) == 0) {
        println("Failed to open file");
        return;
    }

    buf: *char = alloc(char, 1024);

    range_done := false;

    ans := 0;
    for (;;) {
        n := readline(infile, buf);
        if (n == 0) break;

        if (n == 1) {
            range_done = true;
            continue;
        }

        if (range_done) {
            query := cstr_parse_int(buf, n - 1);
            for (i := 0; i < nrange; i += 1) {
                if (lft[i] <= query && query <= rgt[i]) {
                    ans += 1;
                    break;
                }
            }
        } else {
            dashidx: int;
            for (dashidx = 0; dashidx < n; dashidx += 1) {
                if (buf[dashidx] == '-') break;
            }
            if (dashidx == n) {
                println("Missing '-'");
                exit(1);
            }

            lft[nrange] = cstr_parse_int(buf, dashidx);
            rgt[nrange] = cstr_parse_int(buf + dashidx + 1, n - dashidx - 2);

            nrange += 1;
        }
    }

    println(ans);

    file_close(infile);
    delete(buf);
}
